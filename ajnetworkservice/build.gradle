buildscript {
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.29.1"
    }
}
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.jfrog.artifactory'
}
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'
android {
    compileSdk 32

    defaultConfig {
        minSdk 27
        targetSdk 32
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    libraryVariants.all { variant ->
        variant.outputs.all {
            // Do not add Variant (buildType) Name at the end
            outputFileName = "${rootProject.name}-${defaultConfig.versionName}.aar"
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'

    //regionRetrofit Networking related dependencies
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.6.0'
    //endregion

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
//regionPublishing to JFrog Artifactory
afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                artifactId = "${rootProject.name}"
                groupId = "com.ae.ajnetworkservice"
                version = "${android.defaultConfig.versionName}"
                artifact file("$buildDir/outputs/aar/${rootProject.name}-${android.defaultConfig.versionName}.aar")
            }
        }
    }
}
assemble.finalizedBy(artifactoryPublish)
artifactory {
    contextUrl = "${artifactory_context_url}"
    publish {
        repository {
            repoKey = "${artifactory_repo_key}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}
//endregion